plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id "application"

	id "com.google.protobuf" version "0.9.2"
}

version = "0.0.1-SNAPSHOT"

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral( )
}

dependencies {
    // This dependency is used by the application.
    implementation "com.google.guava:guava:31.1-jre"

    // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
	implementation group: "com.google.protobuf", name: "protobuf-java", version: "3.21.10"
	// https://mvnrepository.com/artifact/io.grpc/grpc-all
	implementation group: "io.grpc", name: "grpc-all", version: "1.52.0"

	// https://mvnrepository.com/artifact/javax.annotation/javax.annotation-api
	implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit4 test framework
            useJUnit("4.13.2")
        }
    }
}

application {
    // Define the main class for the application.
    mainClass = "otp.App"
}

// adding the generated code to the source
sourceSets {
	main {
		java {
			srcDirs "build/generated/source/proto/main/grpc"
			srcDirs "build/generated/source/proto/main/java"
		}
	}
}

protobuf {
	// the protoc executable
	protoc {
		artifact= "com.google.protobuf:protoc:3.21.10"
	}

	plugins {
		// grpc Java plugin
		grpc {
			artifact= "io.grpc:protoc-gen-grpc-java:1.52.0"
		}
	}

	generateProtoTasks {
		all( )*.plugins { // use all gRPC plugins to generate code from proto definitions
			grpc { }
		}
	}
}